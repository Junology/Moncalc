/-
Copyright (c) 2022 Jun Yoshida. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
-/

import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Functor.Basic
import Mathlib.CategoryTheory.Functor.Category
import Mathlib.CategoryTheory.NatTrans

import Moncalc.Data.DVect2.Basic
import Moncalc.CategoryTheory.List.Basic

/-!
# The 2-functor structure on `List`

As constructed in `CategoryTheory.List.Basic`, `List Î±` is a `Category` provided so is `Î±`.
Furthermore, it is actually a 2-functor; i.e. there are the following data:

  - A functor `mapF : Functor Î± Î² â¥¤ Functor (List Î±) (List Î²)`, which is defined in [here](CategoryTheory/List/Basic.lean);
  - A natural isomorphism `mapF.unitor : ðŸ­ (List Î±) â‰… mapF.obj (ðŸ­ Î±)`;
  - A natural isomorphism `mapF.compositor : mapF.obj F â‹™ mapF.obj â‰… mapF.obj (F â‹™ G)`.

These are supposed to satisfy the following conditions:

  - `mapF.naturalityâ‚‚ : âˆ€ {Fâ‚ Fâ‚‚ : Î± â†’ Î²} {Gâ‚ Gâ‚‚ : Î² â†’ Î³} (Ï† : Fâ‚ âŸ¶ Fâ‚‚) (Ïˆ : Gâ‚ â†’ Gâ‚‚), (mapF.map Ï† â—« mapF.map Ïˆ) â‰« mapF.compositor Fâ‚‚ Gâ‚‚ = mapF.compositor Fâ‚ Gâ‚ â‰« mapF.map (Ï† â—« Ïˆ)` as natural transformations `mapF.obj Fâ‚ â‹™ mapF.obj Gâ‚ âŸ¶ mapF.obj (Fâ‚‚ â‹™ Gâ‚‚)`.
  - the coherences for `mapF.unitor` and `mapF.compositor`.
-/

namespace CategoryTheory.List

universe u v w

namespace mapF

/-!
## Unitor

A natural isomorphism `mapF.obj (ðŸ­ Î±) â‰… ðŸ­ (List Î±)` that realizes the equality `mapF.obj_id`.
-/
section unitor

variable {Î± : Type u} [Category Î±]

--- The body of the unitor in the "lax" functor direction
protected
def unitorLax : (as : List Î±) â†’ Hom as ((mapF.obj (ðŸ­ Î±)).obj as)
| [] => DVect2.nil
| (a::as) => DVect2.cons (ðŸ™ a) (mapF.unitorLax as)

--- The body of the unitor in the "oplax" functor direction
protected
def unitorOplax : (as : List Î±) â†’ Hom ((mapF.obj (ðŸ­ Î±)).obj as) as
| [] => DVect2.nil
| (a::as) => DVect2.cons (ðŸ™ a) (mapF.unitorOplax as)

--- The lax unitor is a right inverse to the oplax unitor.
@[simp]
protected
theorem unitor_lax_oplax (as : List Î±) : mapF.unitorLax as â‰« mapF.unitorOplax as = ðŸ™ as := by
  induction as
  case nil => rfl
  case cons a as h_ind =>
    dsimp [mapF.unitorLax, mapF.unitorOplax, List.id, DVect2.fromList]
    rw [h_ind, Category.id_comp]

--- The lax unitor is a left inverse to the oplax unitor.
@[simp]
protected
theorem unitor_oplax_lax (as : List Î±) : mapF.unitorOplax as â‰« mapF.unitorLax as = ðŸ™ ((mapF.obj (ðŸ­ Î±)).obj as) := by
  induction as
  case nil => rfl
  case cons a as h_ind =>
    dsimp [mapF.unitorLax, mapF.unitorOplax, List.id, DVect2.fromList]
    rw [h_ind, Category.id_comp]

--- The unitor of the 2-functor `(List, List.mapF)`.
protected
def unitor : ðŸ­ (List Î±) â‰… mapF.obj (ðŸ­ Î±) where
  hom := {
    app := mapF.unitorLax
    naturality := by
      intro as bs fs
      induction fs
      case nil => rfl
      case cons f fs h_ind =>
        dsimp [mapF, mapF.unitorLax, DVect2.map] at *
        rw [h_ind, Category.id_comp, Category.comp_id]
  }
  inv := {
    app := mapF.unitorOplax
    naturality := by
      intro as bs fs
      induction fs
      case nil => rfl
      case cons f fs h_ind =>
        dsimp [mapF, mapF.unitorOplax, DVect2.map] at *
        rw [h_ind, Category.id_comp, Category.comp_id]
  }
  hom_inv_id := by ext; intros; simp
  inv_hom_id := by ext; intros; simp

end unitor


/-!
## Compositor

A natural isomorphism `mapF.obj F â‹™ mapF.obj G â‰… mapF.obj (F â‹™ G)` that realizes the equality `mapF.obj_comp`.
-/

section compositor

variable {Î± : Type u} [Category Î±] {Î² : Type v} [Category Î²] {Î³ : Type w} [Category Î³] (F : Functor Î± Î²) (G : Functor Î² Î³)

protected
def compositorLax : (as : List Î±) â†’ Hom ((mapF.obj F â‹™ mapF.obj G).obj as) ((mapF.obj (F â‹™ G)).obj as)
| [] => DVect2.nil
| (_::as) => DVect2.cons (ðŸ™ _) (mapF.compositorLax as)

protected
def compositorOplax : (as : List Î±) â†’ Hom ((mapF.obj (F â‹™ G)).obj as) ((mapF.obj F â‹™ mapF.obj G).obj as)
| [] => DVect2.nil
| (_::as) => DVect2.cons (ðŸ™ _) (mapF.compositorOplax as)

@[simp]
protected
theorem compositor_lax_oplax (as : List Î±) : mapF.compositorLax F G as â‰« mapF.compositorOplax F G as = ðŸ™ ((mapF.obj F â‹™ mapF.obj G).obj as) := by
  induction as
  case nil => rfl
  case cons a as h_ind =>
    dsimp [mapF.compositorLax, mapF.compositorOplax]
    rw [h_ind, Category.id_comp]
    rfl

@[simp]
protected
theorem compositor_oplax_lax (as : List Î±) : mapF.compositorOplax F G as â‰« mapF.compositorLax F G as = ðŸ™ ((mapF.obj (F â‹™ G)).obj as) := by
  induction as
  case nil => rfl
  case cons a as h_ind =>
  dsimp [mapF.compositorLax, mapF.compositorOplax]
  rw [h_ind, Category.id_comp]

protected
def compositor : mapF.obj F â‹™ mapF.obj G â‰… mapF.obj (F â‹™ G) where
  hom := {
    app := mapF.compositorLax F G
    naturality := by
      intro as bs fs
      induction fs
      case nil => rfl
      case cons f fs h_ind =>
        dsimp [mapF.compositorLax] at *
        rw [h_ind, Category.id_comp, Category.comp_id]
  }
  inv := {
    app := mapF.compositorOplax F G
    naturality := by
      intro as bs fs
      induction fs
      case nil => rfl
      case cons f fs h_ind =>
        dsimp [mapF.compositorOplax]
        rw [h_ind, Category.id_comp, Category.comp_id]
        rfl
  }
  hom_inv_id := by ext; intros; simp
  inv_hom_id := by ext; intros; simp

end compositor


/-!
## Coherences
-/

--- Compatibility of `mapF.compositor` with the horizontal composition `hcomp`.
protected
theorem naturalityâ‚‚ {Î± : Type u} [Category Î±] {Î² : Type v} [Category Î²] {Î³ :Type w} [Category Î³] {Fâ‚ Fâ‚‚ : Functor Î± Î²} {Gâ‚ Gâ‚‚ : Functor Î² Î³} (Ï† : Fâ‚ âŸ¶ Fâ‚‚) (Ïˆ : Gâ‚ âŸ¶ Gâ‚‚) : (mapF.map Ï† â—« mapF.map Ïˆ) â‰« (mapF.compositor Fâ‚‚ Gâ‚‚).hom = (mapF.compositor Fâ‚ Gâ‚).hom â‰« mapF.map (Ï† â—« Ïˆ) := by
  ext as
  induction as
  case nil => rfl
  case cons a as h_ind =>
    dsimp [mapF.compositor, mapF.compositorLax] at *
    rw [h_ind, Category.id_comp, Category.comp_id]

--! TO DO: more coherences

end mapF

end CategoryTheory.List
