/-
Copyright (c) 2022 Jun Yoshida. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
-/

import Mathlib.CategoryTheory.Category.Basic
import Mathlib.CategoryTheory.Functor.Basic

import Moncalc.Data.DVect2.Defs
import Moncalc.Data.DVect2.Basic

/-!
# Category structure on `List`

We show that `List Î±` admits a canonical structure of a category provided `Category Î±`
-/

namespace CategoryTheory.List

universe u v


/-!
## Definition of the structure
-/
section Category

variable {Î± : Type u} [Category Î±]

/--
`Hom [aâ‚,aâ‚‚, ..., aâ‚˜] [bâ‚,bâ‚‚, ..., bâ‚™]` is defined so that
  - if `m=n`, then it is isomorphic to `Hom aâ‚ bâ‚ Ã— Hom aâ‚‚ bâ‚‚ Ã— â‹¯ Ã— Hom aâ‚˜ bâ‚™`;
  - otherwise, it is the empty type.
 -/
 @[reducible]
def Hom : List Î± â†’ List Î± â†’ Type _ :=
  DVect2 Quiver.Hom

protected
def id : (as : List Î±) â†’ Hom as as :=
  DVect2.fromList (Î³:=Quiver.Hom) CategoryStruct.id

--- Due to `Mathlib4` convention, `comp f g` applies `f` "before" `g`.
protected
def comp : {as bs cs : List Î±} â†’ Hom as bs â†’ Hom bs cs â†’ Hom as cs
| [], [], [], DVect2.nil, DVect2.nil => DVect2.nil
| (_::_), (_::_), (_::_), DVect2.cons f fs, DVect2.cons g gs => DVect2.cons (f â‰« g) (List.comp fs gs)

/-!
### Lemmas for recursion
-/
@[simp]
protected
theorem id_nil' : List.id (Î±:=Î±) [] = DVect2.nil := rfl

@[simp]
protected
theorem id_cons' : âˆ€ {a : Î±} {as : List Î±}, List.id (a::as) = DVect2.cons (ðŸ™ a) (List.id as) := rfl

@[simp]
protected
theorem comp_nil' : List.comp (Î±:=Î±) DVect2.nil DVect2.nil = DVect2.nil := rfl

@[simp]
protected
theorem comp_cons' : âˆ€ {a b c : Î±} {as bs cs : List Î±} {f : Quiver.Hom a b} {fs : Hom as bs} {g : Quiver.Hom b c} {gs : Hom bs cs}, List.comp (DVect2.cons f fs) (DVect2.cons g gs) = DVect2.cons (f â‰« g) (List.comp fs gs) := by
  intros; rfl

--- Right unitality of the composition in `List Î±`; see the comment on `comp` function for the composition order.
protected
theorem id_comp : âˆ€ {as bs : List Î±} (fs : Hom as bs), List.comp (List.id as) fs = fs
| [], [], DVect2.nil => rfl
| (_::_), (_::_), DVect2.cons f fs => by
  dsimp
  rw [Category.id_comp, List.id_comp fs]

-- The left unitality of the composition in `List Î±`; see the comments on `comp` function for the composition order.
protected
theorem comp_id : âˆ€ {as bs : List Î±} (fs : Hom as bs), List.comp fs (List.id bs) = fs
| [], [], DVect2.nil => rfl
| (_::_), (_::_), DVect2.cons f fs => by
  dsimp
  rw [Category.comp_id, List.comp_id fs]

-- The associativity of the composition in `List Î±`.
protected
theorem assoc : âˆ€ {as bs cs ds : List Î±} (fs : Hom as bs) (gs : Hom bs cs) (hs : Hom cs ds), List.comp (List.comp fs gs) hs = List.comp fs (List.comp gs hs)
| [], [], [], [], DVect2.nil, DVect2.nil, DVect2.nil => rfl
| (_::_), (_::_), (_::_), (_::_), DVect2.cons f fs, DVect2.cons g gs, DVect2.cons h hs => by
  dsimp
  rw [Category.assoc, List.assoc fs gs hs]

-- `List Î±` is a `Category` provided `Î±` is.
instance instCategoryList : CategoryTheory.Category (List Î±) where
  Hom := Hom
  id := List.id
  comp := List.comp
  id_comp := List.id_comp
  comp_id := List.comp_id
  assoc := List.assoc


/-!
### Lemmas for recursion that do not destruct the `CategoryTheory` notations
-/

@[simp]
theorem id_nil : ðŸ™ (List.nil (Î±:=Î±)) = DVect2.nil := rfl

@[simp]
theorem id_cons : âˆ€ {a : Î±} {as : List Î±}, ðŸ™ (a::as) = DVect2.cons (ðŸ™ a) (ðŸ™ as) := rfl

attribute [reducible] List.Hom

@[simp]
theorem comp_nil : CategoryStruct.comp (obj:=List Î±) (X:=[]) (Y:=[]) (Z:=[]) DVect2.nil DVect2.nil = DVect2.nil := rfl

@[simp]
theorem comp_cons : âˆ€ {a b c : Î±} {as bs cs : List Î±} {f : a âŸ¶ b} {fs : as âŸ¶ bs} {g : b âŸ¶ c} {gs : bs âŸ¶ cs}, CategoryStruct.comp (obj:=List Î±) (X:=a::as) (Y:=b::bs) (Z:=c::cs) (DVect2.cons f fs) (DVect2.cons g gs) = DVect2.cons (f â‰« g) (fs â‰« gs) := rfl

end Category


/-!
## Functorial `append`/`hAppend`
-/
namespace appendF

/--
  `DVect2.append` enables `Hom asâ‚ bsâ‚ â†’ Hom asâ‚‚ bsâ‚‚ â†’ Hom (asâ‚++asâ‚‚) (bsâ‚++bsâ‚‚)` for `asâ‚ asâ‚‚ bsâ‚ bsâ‚‚ : List Î±`. 
-/
instance instHAppendHom (Î± : Type u) [Category Î±] {asâ‚ asâ‚‚ bsâ‚ bsâ‚‚ : List Î±} : HAppend (asâ‚ âŸ¶ bsâ‚) (asâ‚‚ âŸ¶ bsâ‚‚) ((asâ‚++asâ‚‚) âŸ¶ (bsâ‚++bsâ‚‚)) :=
  inferInstanceAs $ HAppend (DVect2 (Quiver.Hom (V:=Î±)) asâ‚ bsâ‚) (DVect2 (Quiver.Hom (V:=Î±)) asâ‚‚ bsâ‚‚) _

variable {Î± : Type u} [Category Î±]

@[simp]
protected
lemma map_cons {a b : Î±} {asâ‚ asâ‚‚ bsâ‚ bsâ‚‚ : List Î±} (f : a âŸ¶ b) (fsâ‚ : asâ‚ âŸ¶ bsâ‚) (fsâ‚‚ : asâ‚‚ âŸ¶ bsâ‚‚) : HAppend.hAppend (Î±:=(a::asâ‚) âŸ¶ (b::bsâ‚)) (DVect2.cons f fsâ‚) fsâ‚‚ = DVect2.cons f (fsâ‚ ++ fsâ‚‚) := rfl

@[simp]
protected
theorem map_id {as bs : List Î±} : ðŸ™ as ++ ðŸ™ bs = ðŸ™ (as++bs) := by
  induction as
  case nil => rfl
  case cons a as h_ind =>
    dsimp
    rw [DVect2.cons_append, h_ind]

@[simp]
protected
theorem map_comp : âˆ€ {asâ‚ asâ‚‚ bsâ‚ bsâ‚‚ csâ‚ csâ‚‚ : List Î±} (fsâ‚ : Hom asâ‚ bsâ‚) (fsâ‚‚ : Hom asâ‚‚ bsâ‚‚) (gsâ‚ : Hom bsâ‚ csâ‚) (gsâ‚‚ : Hom bsâ‚‚ csâ‚‚), (fsâ‚ ++ fsâ‚‚) â‰« (gsâ‚ ++ gsâ‚‚) = HAppend.hAppend (self:=instHAppendHom Î±) (fsâ‚ â‰« gsâ‚) (fsâ‚‚ â‰« gsâ‚‚)
| [], _, [], _, [], _, DVect2.nil, _, DVect2.nil, _ => rfl
| (_::_), _, (_::_), _, (_::_), _, DVect2.cons f fsâ‚, fsâ‚‚, DVect2.cons g gsâ‚, gsâ‚‚ => by
  rw [DVect2.cons_append, DVect2.cons_append]
  dsimp
  rw [appendF.map_comp fsâ‚ _ gsâ‚ _, DVect2.cons_append]

end appendF


/-!
## Misceleneous lemmas
-/

--- Functional extensionality for functors into `List Î±`
theorem eqF_List {Î± : Type u} [Category Î±] {Î² : Type v} [Category Î²] : (F G : Functor Î± (List Î²)) â†’ (âˆ€ (a : Î±), F.obj a = G.obj a) â†’ (âˆ€ (aâ‚ aâ‚‚ : Î±) (f : aâ‚ âŸ¶ aâ‚‚), DVect2.Eq (F.map f) (G.map f)) â†’ F = G
| Functor.mk F _ _, Functor.mk G _ _ => by
  cases F with | mk F_obj F_map =>
  cases G with | mk G_obj G_map =>
  intro hobj
  have : F_obj = G_obj := funext hobj
  cases this
  dsimp
  intro hmap
  have : @F_map = @G_map := by
    apply funext; intro a; apply funext; intro b
    apply funext
    intro f
    exact (hmap a b f).eq_of
  cases this
  rfl

end CategoryTheory.List